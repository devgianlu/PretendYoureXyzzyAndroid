plugins {
    id 'com.github.triplet.play' version '2.5.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'

def isTravis = System.getenv("CI") == "true"
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.gianlu.pretendyourexyzzy"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 98
        versionName "3.1.4"
    }

    if (isTravis) {
        signingConfigs {
            release {
                storeFile file("../.travis/keystore.jks")
                storePassword System.getenv("keystore_storePsswd")
                keyAlias = System.getenv("keystore_keyAlias")
                keyPassword System.getenv("keystore_keyPsswd")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isTravis) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled true
                    shrinkResources true
                }
            }
        }

        foss {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled false
                    shrinkResources false
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'MissingTranslation'
    }

    playConfigs {
        foss {
            commit = false
            enabled = false
        }
    }
}

play {
    defaultToAppBundles = true
    enabled = isTravis
    serviceAccountCredentials = file("../.travis/secrets.json")
    track = "production"
    releaseStatus = "draft"
}

dependencies {
    implementation project(':CommonUtils')
    implementation 'com.github.AppIntro:AppIntro:6.0.0'
    implementation 'me.zhanghai.android.materialratingbar:library:1.4.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.squareup.okhttp3:okhttp:4.6.0'
    implementation 'org.conscrypt:conscrypt-android:2.2.1'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'com.google.android.gms:play-services-games:19.0.0'
    implementation 'com.google.android.gms:play-services-auth:18.0.0'

    standardImplementation 'com.google.firebase:firebase-perf:19.0.7'
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

apply plugin: 'com.google.gms.google-services'