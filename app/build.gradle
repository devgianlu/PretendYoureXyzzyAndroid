plugins {
    id 'com.github.triplet.play' version '3.7.0'
}

apply plugin: 'com.android.application'

def isCi = System.getenv("CI") == "true"
android {
    compileSdkVersion 30
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.gianlu.pretendyourexyzzy"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 136
        versionName "5.1.2"
    }

    if (isCi) {
        signingConfigs {
            release {
                storeFile file("../.github/keystore.jks")
                storePassword System.getenv("KEYSTORE_STORE_PASS")
                keyAlias = System.getenv("KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("KEYSTORE_KEY_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isCi) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled true
                    shrinkResources true
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'MissingTranslation', 'Instantiatable'
    }
}

play {
    defaultToAppBundles.set(true)
    enabled.set(isCi)
    serviceAccountCredentials.set(file("../.github/secrets.json"))
    track.set("production")
    releaseStatus.set(com.github.triplet.gradle.androidpublisher.ReleaseStatus.DRAFT)
}

dependencies {
    implementation project(':CommonUtils')
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'

    implementation 'com.github.bumptech.glide:glide:4.13.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.13.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    implementation('com.google.firebase:firebase-perf:20.0.6') {
        exclude group: 'com.google.protobuf'
    }

    implementation project(':pyxoverloaded')
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'