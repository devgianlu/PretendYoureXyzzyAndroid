import com.github.triplet.gradle.androidpublisher.ReleaseStatus

plugins {
    id 'com.github.triplet.play' version '3.8.4'
}

apply plugin: 'com.android.application'

def isCi = System.getenv("CI") == "true"
android {
    defaultConfig {
        compileSdk 34

        applicationId "com.gianlu.pretendyourexyzzy"
        minSdkVersion 21
        targetSdkVersion 34

        versionCode 140
        versionName "5.1.6"
    }

    if (isCi) {
        signingConfigs {
            release {
                storeFile file("../.github/keystore.jks")
                storePassword System.getenv("KEYSTORE_STORE_PASS")
                keyAlias = System.getenv("KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("KEYSTORE_KEY_PASS")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isCi) signingConfig signingConfigs.release
        }
    }

    flavorDimensions = ['main']
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled true
                    shrinkResources true
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        warning 'MissingTranslation', 'Instantiatable'
    }
}

play {
    defaultToAppBundles.set(true)
    enabled.set(isCi)
    serviceAccountCredentials.set(file("../.github/secrets.json"))
    track.set("production")
    releaseStatus.set(ReleaseStatus.DRAFT)
}

dependencies {
    implementation project(':CommonUtils')
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'

    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'

    implementation('com.google.firebase:firebase-perf:20.5.1') {
        exclude group: 'com.google.protobuf'
    }

    implementation project(':pyxoverloaded')
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'