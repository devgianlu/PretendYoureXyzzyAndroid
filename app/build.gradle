plugins {
    id 'com.github.triplet.play' version '2.6.2'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'

def isTravis = System.getenv("CI") == "true"
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.gianlu.pretendyourexyzzy"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 116
        versionName "4.2.3"
    }

    if (isTravis) {
        signingConfigs {
            release {
                storeFile file("../.travis/keystore.jks")
                storePassword System.getenv("keystore_storePsswd")
                keyAlias = System.getenv("keystore_keyAlias")
                keyPassword System.getenv("keystore_keyPsswd")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (isTravis) signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'main'
    productFlavors {
        standard {
            dimension 'main'

            buildTypes {
                release {
                    minifyEnabled true
                    shrinkResources true
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        warning 'MissingTranslation'
    }

    playConfigs {
        foss {
            commit = false
            enabled = false
        }
    }
}

play {
    defaultToAppBundles = true
    enabled = isTravis
    serviceAccountCredentials = file("../.travis/secrets.json")
    track = "production"
    releaseStatus = "draft"
}

dependencies {
    implementation project(':CommonUtils')
    implementation 'com.github.AppIntro:AppIntro:6.0.0'
    implementation 'me.zhanghai.android.materialratingbar:library:1.4.0'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'com.google.android.material:material:1.3.0-alpha04'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'org.conscrypt:conscrypt-android:2.5.1'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    implementation('com.google.firebase:firebase-perf:19.0.10') {
        exclude group: 'com.google.protobuf'
    }

    implementation project(':pyxoverloaded')
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'